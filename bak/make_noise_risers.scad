// make noise case 60deg riser wedge
//
// case dimensions: 
//   130.55mm x 69.85mm x 546.1mm
//
w = 26; // width of riser
x = 130.55; // case dimensions
y = 546.10; 
z =  69.85;
z_alt = z + 100; // inflated depth to clear overlap

t = -80; // angle of rotation, 'theta', in degrees
r = 20; // rise in mm

module case() {
  translate([0, 0, r]) // raised
    rotate([0, t, 0]) // rotated
      cube([x, y, z]);
}

module riser(x_add) {
  // x-offset generated by rotation of z component, plus additional x-offset
  x_z_offset = -sin(t) * z + x_add; 
  x_y_offset = cos(t) * x; // x-offset generated by rotation of y component
  z_x_offset = -sin(t) * x; // z-offset generated by rotation of x component

  difference() {
    // encapsulating volume
      translate([-x_z_offset, 0, 0])
        cube([x_z_offset + x_y_offset, w, z_x_offset]);

    // the case, tilted and raised.
    // exaggerated depth to clear front panel
    translate([0, 0, r])
    rotate([0, t, 0])
      cube([x, y, z_alt]);
  }
}

// currently manually scaled/hollowed
// s/b calculated from riser shape and angle
module hollow_riser() {
  difference() {
    riser(0);
    translate([4, 0, 2])
      scale([0.6, 1.1, 0.6])
        riser(40);
  }
}

// case();
// riser(0);
hollow_riser();
